apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId 'org.traccar.client'
        buildConfigField 'boolean', 'HIDDEN_APP', 'false'
        minSdkVersion 16
        targetSdkVersion 32
        versionCode 78
        versionName '6.17'
        multiDexEnabled true
    }
    namespace 'org.traccar.client'

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault = true
        }
        google
        hidden {
            applicationId 'org.traccar.client.hidden'
            buildConfigField 'boolean', 'HIDDEN_APP', 'true'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        checkReleaseBuilds false
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def lifecycle_version = "2.6.0-alpha01"

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.test:core-ktx:1.4.0'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.judemanutd:autostarter:1.1.0'

    //Datastore Preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // LiveData
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version")
    // Lifecycles only (without ViewModel or LiveData)
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation('dev.doubledot.doki:library:0.0.1@aar') {
        transitive = true
    }
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    googleImplementation platform('com.google.firebase:firebase-bom:28.2.1')
    googleImplementation 'com.google.firebase:firebase-core'
    googleImplementation 'com.google.firebase:firebase-analytics'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-location:20.0.0'
    googleImplementation 'com.google.android.play:core:1.10.3'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    task copyJson(type: Copy) {
        from '../../environment/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
